/*
  Conversation avec Muybridge
  Code d'origine : Michel Glaize
  Modifications : R√©mi Sarrailh (¬µsini)
  *************************************
  **** Gestion de la logique       ****
  *************************************
*/

#include "Arduino.h"
#include "parametres.h" // Les param√®tres
#include "moteurs.h" // Fonctions du moteurs et des fin de courses
#include "reseau.h" // Fonctions pour le r√©seau WiFi et le protocole UDP
#include "osc.h" // Fonctions pour le protocole OSC (par UDP)
#include "capteurs.h" // Fonctions li√©s au capteur LIDAR Garmin

TaskHandle_t Coeur0;

unsigned long temps_MAJ_position = 0;
unsigned long temps_MAJ_images = 0;
unsigned long temps_MAJ_Moteur = 0;

// Cette fonction v√©rifie si une personne est pr√©sente sur le parcours
void verification_Absence() {
  // Si personne n'est detect√© sur le parcours
  if(etape == -1) {
    // Mais que le parcours est en cours d'utilisation
    if(depart_Parcours == 1) {
        Serial.print("[üëÄ] Personne sur le parcours depuis : ");
        Serial.println(millis() - temps_Absence);
        // Et que le delai d'absence (d√©fini dans parametres.h) est d√©pass√©
        if((millis() - temps_Absence) > delai_Absence) {
          // Activation de la r√©initialisation de la s√©quence
          reinitialisationSequence = true;
      }
    }
  }
}

// Cette fonction temporise le changement des images projet√©es
void changement_Etape() {
  // Si une personne est pr√©sent sur le parcours
  if(etape != -1) {
    Serial.println("[üî¥] Etape :" + String(etape));
    Serial.println("[üöÇ] D√©placement vers :" + String(tableau_Position_Moteur[etape]));
    
    moteur.moveTo(-tableau_Position_Moteur[etape]); // D√©placer le train
    absence_Utilisateur = 0; // Remis √† z√©ro du compteur d'absence
    
    // Envoi de l'ordre de changer d'image √† Millumin
    String sequence_Etape = sequence + String(etape);
    
    // ‚è≤ Chrono Millumin
    // Selon la s√©quence, les mises √† jour sont plus ou moins rapide
    // Pour laissez le temps de r√©actualiser l'animation
    if((millis() - temps_MAJ_images) > delai_MAJ_Millumin[sequenceID]) {

      // Envoi OSC de la s√©quence √† changer
      envoi_Par_OSC(IP_Mac, port_Millumin, adresse_Millumin, sequence_Etape);
      Serial.print("[üíª] Millumin --> ");
      Serial.println(sequence_Etape);
      temps_MAJ_images = millis(); // Remise √† z√©ro du chrono
    }

    // Mis √† jour de Processing, si l'√©tape est sup√©rieur √† l'√©tape effectu√©
    // L'image g√©n√©r√© par Processing, ne revient pas en arri√®re
    if(etape >= etape_Effectue) {
      // Mis √† jour de la dernier √©tape effectu√©
      etape_Effectue = etape;
      
      // Envoi de la s√©quence √† Processing
      envoi_Par_OSC(IP_Mac, port_Processing, (char *)"", sequence_Etape);
      Serial.print("[üíª] Processing --> ");
      Serial.println(sequence_Etape);
    }
  }
}

// Cette fonction envoie √† l'IPAD la position du train
void maj_Position_Moteur() {
  // Affiche la position du moteur tout les delai_MAJ_Moteur (d√©fini dans param√®tres.h)
  if((millis() - temps_MAJ_Moteur) > delai_MAJ_Moteur) {
    temps_MAJ_Moteur = millis();
    int position_Moteur = -moteur.currentPosition();
    //Serial.println(position_Moteur);
    
    if(position_Moteur < 0) { position_Moteur = 0;} // Si la position du moteur est inf√©rieur √† 0 alors la mettre √† z√©ro

    // Transformer la position du moteur en une valeur flotante de 0 √† 1
    float position_Moteur_Tablette = mapfloat(position_Moteur, 0, position_Train_Maximal, 0, 1);

    // Si la position du moteur a chang√©, la mettre √† jour
    if(moteur.distanceToGo() != 0) {
      // Envoi du message par OSC sur l'application TouchOSC sur l'IPAD 
      envoi_Par_OSC(IP_iPad, port_Ipad, adresse_Position_Train, position_Moteur_Tablette);
      Serial.print("[üì±] Envoi Position Moteur: ");
      Serial.println(position_Moteur);
    }
  }
}

// üü† Coeur 0 de l'ESP32
// Afin de ne pas entraver le mouvement du moteur, toute la logique est concentr√© dans le CORE 0 de l'ESP32
// Comme expliquer ici : https://valarsystems.com/blogs/news/how-to-use-accelstepper-with-wifi-on-an-esp32
void Taches_Coeur0( void * pvParameters ) {
  // Boucle Infini
  for (;;) {
      // L'utilisateur choisit la s√©quence (quand le parcours est r√©initialis√© ou en cours de r√©initialisation)
    if( !sequence_Choisi ) {
      // üì± R√©ception du num√©ro de la s√©quence depuis la tablette si la r√©ception est activ√©e.
      receptionSequence();
    }
    // Si la s√©quence n'est pas en cours de r√©initialisation
    if ( !reinitialisationSequence ) {
      // üéû La s√©quence est choisi et le train a un z√©ro connu
      // On r√©cup√®re la position de l'utilisateur
      if( sequence_Choisi ) {

        // Test avec Simulateur Capteur
        //if(!retour_Train) {
          //receptionSequence();
        //}

        // Temporisation des mise √† jour des positions
        if((millis() - temps_MAJ_position) > delai_MAJ_Position) {
          temps_MAJ_position = millis(); // ‚è± Red√©marrage du chrono Position
          if(!retour_Train) { // üöÇ Si le train n'est pas en train de rentrer √† la gare
            recuperation_Position_Utilisateur(); // üëÄ R√©cup√©ration de la position de l'utilisateur
          }
          if(etape != etape_Precedente) { // Si l'√©tape est diff√©rente de l'√©tape pr√©c√©dente
            if(nouvelle_Etape) {
                nouvelle_Etape = false;
                changement_Etape(); // Changer d'√©tape
            }
            etape_Precedente = etape; // Mettre √† jour l'√©tape avec l'√©tape pr√©c√©dente
          }
          verification_Absence(); // V√©rification si l'utilisateur est absent du parcours (apr√®s qu'il soit initialis√©)
        }
      }
    } else { // Si le parcours est en cours de r√©initialisation
      if (!retour_Train) {
          remise_Zero_Train(); // üöÇ Ramener le train √† la gare
          envoi_Par_OSC(IP_Mac, port_Millumin, adresse_Millumin, message_Repos); // Envoi du message Repos sur Millumin
          envoi_Par_OSC(IP_Mac, port_Processing, (char *)"", message_Repos); // Envoi du message Repos sur Processing
          envoi_Par_OSC(IP_iPad, port_Ipad, adresse_Etat_Parcours, "√Ä votre Tour !"); // Envoi du message Pr√™t sur l'IPAD
          Udp.flush();
          Udp.stop();
          Udp.begin(port_ESP32);
      }
    }
    maj_Position_Moteur(); // Mis √† jour de la position du moteur
  }
}

// Param√©trages du Syst√®me
void setup() {
  Serial.begin(115200); // Mise en route de la communication de d√©bogage s√©rie

  // D√©active le WatchDog du Coeur 0 de l'ESP32 comme sp√©cifi√© ici
  // https://valarsystems.com/blogs/news/how-to-use-accelstepper-with-wifi-on-an-esp32

  parametrages_Reseau(); // Mise en route de la connexion
  parametrages_Moteur(); // Param√®trage du moteur et du fin de course
  parametrages_Capteurs(); // Param√©trage des capteurs LIDAR

  Serial.println("[üî¥] Cr√©ation de la t√¢che logique sur le coeur 0");
  disableCore0WDT();
  xTaskCreatePinnedToCore(Taches_Coeur0, "Coeur_0", 10000, NULL, 1, &Coeur0, 0);
}


// Dans le loop (coeur 1) on ne s'occupe que du moteur et du fin de courses, afin d'√™tre s√ªr que accelstepper donne
// bien l'ordre de faire tout les pas.
void loop() {
  // Si le train n'est pas en train de revenir et qu'aucune s√©quence n'est choisi
  verification_Fin_De_Courses(); // V√©rification de l'√©tat du capteur de fin de courses
  moteur.run(); // D√©place le moteur s'il n'est pas √† sa position demand√©

  //Serial.println(moteur.currentPosition());
}
